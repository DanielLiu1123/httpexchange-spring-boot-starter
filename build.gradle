plugins {
    id 'java-library'
    id 'com.diffplug.spotless' version "${spotlessVersion}"
    id 'com.github.spotbugs' version "${spotbugsVersion}"
}

repositories {
    mavenCentral()
    maven {
        url "https://repo.spring.io/snapshot"
    }
    maven {
        url "https://repo.spring.io/milestone"
    }
}

dependencies {
    api(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
    annotationProcessor(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
    compileOnly(platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"))
    testImplementation(platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"))

    api("org.springframework.boot:spring-boot-starter")
    api("org.springframework:spring-webflux")

    // dynamic refresh configuration for exchange clients
    compileOnly("org.springframework.cloud:spring-cloud-context")

    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("com.freemanan:classpath-replacer-junit5:${classpathReplacerVersion}")

    testCompileOnly("org.springframework.boot:spring-boot-starter-web")
    testImplementation("org.springframework.boot:spring-boot-starter-webflux")
    testImplementation("org.springframework.boot:spring-boot-starter-validation")
    testImplementation("org.springframework.cloud:spring-cloud-context")
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

test {
    useJUnitPlatform()
}

spotless {
    encoding 'UTF-8'
    java {
        toggleOffOn()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        palantirJavaFormat()

        custom('Refuse wildcard imports', {
            if (it =~ /\nimport .*\*;/) {
                throw new IllegalStateException("Do not use wildcard imports, 'spotlessApply' cannot resolve this issue, please fix it manually.")
            }
        } as Closure<String>)
    }
}

spotbugs {
    spotbugsTest.enabled = false
    omitVisitors.addAll('FindReturnRef', 'MethodReturnCheck')
}

tasks.register('installGitHook', Copy) {
    from "$rootProject.rootDir/.githooks"
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}
build.dependsOn installGitHook

apply from: "${rootDir}/gradle/deploy.gradle"
